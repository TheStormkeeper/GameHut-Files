;----------------------------------------------------------
;		FLAME CODE
;----------------------------------------------------------
		RSSET	0
FLAME_X:	RS.W	1
FLAME_DIR:	RS.W	1
FLAME_TIME:	RS.W	1
FLAME_SIZE:	RS.B	0

		RSSET	RINGEND
;FLAME STUFF
FBUFFER:	RS.W	1	;BUFFER FOR FLAME
XPOS:		RS.W	1	;XPOS FOR FLAME

FLAMES:		EQU	12	;TOTAL NUMBER OF FLAMES
FLAMEDATA:	RS.B	FLAMES*FLAME_SIZE

FLAMEEND:	RS.B	0

FLAMETABLE:	EQU	DECRUNCH+4096+1160*2
FLAMEBUF1:	EQU	DECRUNCH+4096
FLAMEBUF2:	EQU	DECRUNCH+4096+1160

;----------------------------------------------------------
;		INIT FLAME
;----------------------------------------------------------
INITFLAME:	
		LEA.L	PALETTE3,A0		;FLAME PALETTE
		JSR	SETPAL3

		LEA.L	DECRUNCH,A0		;SET UP MASK FOR FLAME PLAYFIELD
		MOVE.W	#$8000+S_PAL3,D0
		MOVE.W	#(4096/2)-1,D1
@L1:		MOVE.W	D0,(A0)+
		DBRA	D1,@L1

 		LEA.L	DECRUNCH+4096,A0	;CLEAR FLAME BUFFERS
		MOVEQ.L	#0,D0
		MOVE.W	#((1160*2)/4)-1,D1
@L3:		MOVE.L	D0,(A0)+
		DBRA	D1,@L3

;INIT FLAME CREATORS
		LEA.L	FLAMEDATA,A1
		MOVE.W	#FLAMES-1,D1
@L2:		MOVE.W	#-10000,(A1)+		;START
		MOVE.W	#1,(A1)+		;SPEED
		MOVE.W	#0,(A1)+		;LIFE
		DBRA	D1,@L2

		MOVE.W	#0,FBUFFER

		DMADUMP	FLAMEGFX1,32*32,$0	;DUMP 4 CHARACTERS (SIZE 32 BYTES EACH) TO VRAM LOCATION $20 (MAP GRAPHICS)

		BSR	CALCFLAME		;CREATE A TABLE USED TO SPEED UP ANTI-ALIAS MATHS

		RTS
;----------------------------------------------------------
;		CALC FLAME TABLE - TO SPEED UP MATHS
;----------------------------------------------------------
CALCFLAME:	LEA.L	FLAMETABLE,A0
		MOVEQ.L	#0,D0
		MOVEQ.L	#0,D2

		MOVE.W	#255-1,D1

		MOVE.B	#0,(A0)+

@L1:		MOVE.B	D0,D2
		ASR.W	#2,D2

		MOVE.B	D2,(A0)+
		ADD.B	#1,D0
		DBRA	D1,@L1

		RTS

;----------------------------------------------------------
;		CONVERT FLAME - COPY FLAME TO PLAYFIELD
;----------------------------------------------------------
CONVFLAME:	LEA.L	DECRUNCH+1,A0
		LEA.L	FLAMEBUF2,A1
		SUB.W	FBUFFER,A1

		MOVE.W	#$8000+S_PAL3,D1
		MOVE.W	#28-1,D2
@L2:
		REPT	9
		MOVE.L	(A1)+,D1
		MOVEP.L	D1,(0,A0)		;VERY SNEAKY
		LEA.L	8(A0),A0
		ENDR
		MOVE.L	(A1)+,D1
		MOVEP.L	D1,(0,A0)
		LEA.L	-72+128(A0),A0
		DBRA	D2,@L2

		RTS

;----------------------------------------------------------
;		MAIN FLAME CODE
;----------------------------------------------------------
DRAWFLAME:
;DUMP FLAME TO VRAM
		DMADUMP	DECRUNCH,4096,$E000
;TOGGLE BUFFERS
		CMP.W	#0,FBUFFER
		BEQ.S	@B1
		MOVE.W	#0,FBUFFER
		BRA.S	@B2
@B1:		MOVE.W	#1160,FBUFFER
@B2:

;ADD AND MOVE FLAME CREATION OBJECTS
		LEA.L	FLAMEBUF2+40*28,A0
		SUB.W	FBUFFER,A0

		MOVEQ.L	#0,D0			;CLEAR BOTTOM LINE OF FLAME TO AVOID OVER SATURATION
		REPT	10
		MOVE.L	D0,(A0)+
		ENDR

		LEA.L	FLAMEBUF2+40*28+2,A0	
		SUB.W	FBUFFER,A0

		LEA.L	FLAMEDATA,A1
		MOVE.W	#FLAMES-1,D2

		MOVE.W	#$1F,D1

@J1:		MOVE.W	(A1)+,D0		;POSITION
		ADD.W	(A1)+,D0		;SPEED/DIR
		BLT.S	@D1
		CMP.W	#38*8,D0
		BLT.S	@D2
@D1:		LEA.L	2(A1),A1
		BRA.S	@D3
@D2:		MOVE.W	D0,-4(A1)
		ASR.W	#3,D0
		LEA.L	(A0,D0.W),A2

		MOVE.B	D1,-2(A2)		;DRAW CROSS AT RANDOM POSITION
		MOVE.B	D1,-1(A2)
		MOVE.B	D1,-(40+1)(A2)
		MOVE.B	D1,(A2)

		SUB.W	#1,(A1)+
		BGE.S	@T1
@D3:
		BTST	#5,JOYPAD0
		BNE	@SKIP

		MOVE.L	A0,A3
		JSR	GETRANDOM
		ADD.W	#32,D0
		MOVE.W	D0,-6(A1)		;POSITION
		JSR	GETRANDOM
		AND.W	#$1F,D0
		MOVE.W	D0,-2(A1)		;TIME
		JSR	GETRANDOM
		AND.W	#$7,D0
		SUB.W	#3,D0
		MOVE.W	D0,-4(A1)		;SPEED
		MOVE.L	A3,A0
		BRA.S	@T1
@SKIP:		MOVE.W	#-10000,-6(A1)
@T1:
		DBRA	D2,@J1

;NOW ANTI ALIAS TO BUFFER 1
		MOVE.W	FBUFFER,D0
		BEQ.S	@PP1
		LEA.L	FLAMEBUF1+40,A0
		LEA.L	FLAMEBUF2+1,A1
		BRA.S	@PP2

@PP1:		LEA.L	FLAMEBUF2+40-1,A0
		LEA.L	FLAMEBUF1,A1
@PP2:
		LEA.L	FLAMETABLE,A2	;FLAME TABLE
		LEA.L	-39(A0),A3
		LEA.L	2(A0),A4

		MOVEQ.L	#0,D0

		MOVE.W	#(28*40)-1,D5

@LL1:		MOVE.B	(A0)+,D0
		ADD.B	(A0),D0
		ADD.B	(A4)+,D0
		ADD.B	(A3)+,D0
;		ASR.W	#2,D0
;		MOVE.B	D0,(A1)+
		MOVE.B	(A2,D0.W),(A1)+
		DBRA	D5,@LL1

		RTS

;----------------------------------------------------------
;		FLAME GFX
;----------------------------------------------------------
FLAMEGFX1:
		REPT	32
		DC.B	$00
		ENDR

		REPT	4
		DC.L	$01010101
		DC.L	$10101010
		ENDR

		REPT	32
		DC.B	$11
		ENDR

		REPT	4
		DC.L	$12121212
		DC.L	$21212121
		ENDR

		REPT	32
		DC.B	$22
		ENDR

		REPT	4
		DC.L	$23232323
		DC.L	$32323232
		ENDR

		REPT	32
		DC.B	$33
		ENDR

		REPT	4
		DC.L	$34343434
		DC.L	$43434343
		ENDR

		REPT	32
		DC.B	$44
		ENDR

		REPT	4
		DC.L	$45454545
		DC.L	$54545454
		ENDR

		REPT	32
		DC.B	$55
		ENDR

		REPT	4
		DC.L	$56565656
		DC.L	$65656565
		ENDR

		REPT	32
		DC.B	$66
		ENDR

		REPT	4
		DC.L	$67676767
		DC.L	$76767676
		ENDR

		REPT	32
		DC.B	$77
		ENDR

		REPT	4
		DC.L	$78787878
		DC.L	$87878787
		ENDR

		REPT	32
		DC.B	$88
		ENDR

		REPT	4
		DC.L	$89898989
		DC.L	$98989898
		ENDR

		REPT	32
		DC.B	$99
		ENDR

		REPT	4
		DC.L	$9A9A9A9A
		DC.L	$A9A9A9A9
		ENDR

		REPT	32
		DC.B	$AA
		ENDR

		REPT	4
		DC.L	$ABABABAB
		DC.L	$BABABABA
		ENDR

		REPT	32
		DC.B	$BB
		ENDR

		REPT	4
		DC.L	$BCBCBCBC
		DC.L	$CBCBCBCB
		ENDR

		REPT	32
		DC.B	$CC
		ENDR

		REPT	4
		DC.L	$CDCDCDCD
		DC.L	$DCDCDCDC
		ENDR

		REPT	32
		DC.B	$DD
		ENDR

		REPT	4
		DC.L	$DEDEDEDE
		DC.L	$EDEDEDED
		ENDR

		REPT	32
		DC.B	$EE
		ENDR

		REPT	4
		DC.L	$EFEFEFEF
		DC.L	$FEFEFEFE
		ENDR

		REPT	32
		DC.B	$FF
		ENDR

		REPT	32
		DC.B	$FF
		ENDR


FLAMEGFX2:
		REPT	32
		DC.B	$00
		ENDR

		DC.L	$00000000
		DC.L	$00000000
		DC.L	$00000000
		DC.L	$00011000
		DC.L	$00011000
		DC.L	$00000000
		DC.L	$00000000
		DC.L	$00000000

		DC.L	$00000000
		DC.L	$00000000
		DC.L	$00011000
		DC.L	$00122100
		DC.L	$00122100
		DC.L	$00011000
		DC.L	$00000000
		DC.L	$00000000

		DC.L	$00000000
		DC.L	$00011000
		DC.L	$00122100
		DC.L	$01233210
		DC.L	$01233210
		DC.L	$00122100
		DC.L	$00011000
		DC.L	$00000000

		DC.L	$00000000
		DC.L	$00111100
		DC.L	$01233210
		DC.L	$01344310
		DC.L	$01344310
		DC.L	$01233210
		DC.L	$00111100
		DC.L	$00000000

		DC.L	$00011000
		DC.L	$00122100
		DC.L	$01344310
		DC.L	$12455421
		DC.L	$12455421
		DC.L	$01344310
		DC.L	$00122100
		DC.L	$00011000

		DC.L	$00111100
		DC.L	$01233210
		DC.L	$12455421
		DC.L	$13566531
		DC.L	$13566531
		DC.L	$12455421
		DC.L	$01233210
		DC.L	$00111100

		DC.L	$01122110
		DC.L	$12344321
		DC.L	$13566531
		DC.L	$24677642
		DC.L	$24677642
		DC.L	$13566531
		DC.L	$12344321
		DC.L	$01122110

		DC.L	$12233221
		DC.L	$23455432
		DC.L	$24677642
		DC.L	$35777753
		DC.L	$35777753
		DC.L	$24677642
		DC.L	$23455432
		DC.L	$12233221

		DC.L	$23344332
		DC.L	$34566543
		DC.L	$35777753
		DC.L	$46777764
		DC.L	$46777764
		DC.L	$35777753
		DC.L	$34566543
		DC.L	$23344332

		DC.L	$34455443
		DC.L	$45677654
		DC.L	$46777764
		DC.L	$57777775
		DC.L	$57777775
		DC.L	$46777764
		DC.L	$45677654
		DC.L	$34455443

		DC.L	$45566554
		DC.L	$56777765
		DC.L	$57777775
		DC.L	$67777776
		DC.L	$67777776
		DC.L	$57777775
		DC.L	$56777765
		DC.L	$45566554

		DC.L	$56677665
		DC.L	$67777776
		DC.L	$67777776
		DC.L	$77777777
		DC.L	$77777777
		DC.L	$67777776
		DC.L	$67777776
		DC.L	$56677665

		DC.L	$67777776
		DC.L	$77777777
		DC.L	$77777777
		DC.L	$77777777
		DC.L	$77777777
		DC.L	$77777777
		DC.L	$77777777
		DC.L	$67777776

		REPT	8
		DC.L	$77777777
		ENDR

		REPT	4
		DC.L	$78787878
		DC.L	$87878787
		ENDR

		REPT	32
		DC.B	$88
		ENDR

		REPT	4
		DC.L	$89898989
		DC.L	$98989898
		ENDR

		REPT	32
		DC.B	$99
		ENDR

		REPT	4
		DC.L	$9A9A9A9A
		DC.L	$A9A9A9A9
		ENDR

		REPT	32
		DC.B	$AA
		ENDR

		REPT	4
		DC.L	$ABABABAB
		DC.L	$BABABABA
		ENDR

		REPT	32
		DC.B	$BB
		ENDR

		REPT	4
		DC.L	$BCBCBCBC
		DC.L	$CBCBCBCB
		ENDR

		REPT	32
		DC.B	$CC
		ENDR

		REPT	4
		DC.L	$CDCDCDCD
		DC.L	$DCDCDCDC
		ENDR

		REPT	32
		DC.B	$DD
		ENDR

		REPT	4
		DC.L	$DEDEDEDE
		DC.L	$EDEDEDED
		ENDR

		REPT	32
		DC.B	$EE
		ENDR

		REPT	4
		DC.L	$EFEFEFEF
		DC.L	$FEFEFEFE
		ENDR

		REPT	32
		DC.B	$FF
		ENDR

		REPT	32
		DC.B	$FF
		ENDR
